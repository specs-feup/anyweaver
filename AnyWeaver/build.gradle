plugins {
    id 'distribution'
    id 'application' // Executable
    id 'java' // Java project
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    withSourcesJar()
}

// Repositories providers
repositories {
    mavenCentral()
}

configurations {
    weaverGeneratorRuntime
}

dependencies {
    testImplementation "junit:junit:4.13.1"


    implementation ":jOptions"
    implementation ":SpecsUtils"

    implementation ":LanguageSpecification"
    implementation ":LaraUtils"
    implementation ":LARAI"
    implementation ":WeaverInterface"

    implementation ":AnyAst"

    weaverGeneratorRuntime ":WeaverGenerator"
}

// Project sources
sourceSets {
    main {
        java {
            srcDir 'src'
        }

        resources {
            srcDir 'resources'
        }
    }
}

// Weaver Generator
tasks.register('generateWeaver', JavaExec) {
    group = "Execution"
    description = "Generates the Weaver Abstracts from the Language Specification"
    classpath = configurations.weaverGeneratorRuntime
    mainClass = 'org.lara.interpreter.weaver.generator.commandline.WeaverGenerator'
    args = [
        '-w', 'AnyWeaver',
        '-x', './resources/anycompiler/weaverspecs',
        '-o', './src',
        '-p', 'pt.up.fe.specs.anycompiler.weaver',
        '-n', 'pt.up.fe.specs.anycompiler.ast.AnyNode',
        '-j',
        '-e'
    ]
}

compileJava.dependsOn generateWeaver
